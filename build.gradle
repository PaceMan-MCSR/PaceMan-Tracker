plugins {
    id 'java'
}

group = project.maven_group
version = project.version
archivesBaseName = project.archives_base_name

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url "https://www.jetbrains.com/intellij-repository/releases" }
    maven { url "https://maven.fabricmc.net/" }
}

configurations {
    // provided configuration defines configurations which should also be included in the jar
    provided
    implementation.extendsFrom(provided)
}

dependencies {
    // Choose Julti version: https://jitpack.io/#DuncanRuns/Julti/
    implementation 'com.github.DuncanRuns:Julti:v1.6.0'
    // Choose Jingle version: https://jitpack.io/#DuncanRuns/Jingle/
    implementation 'com.github.DuncanRuns:Jingle:eeb4291b13'

    provided 'com.jetbrains.intellij.java:java-gui-forms-rt:203.7148.30'
    provided 'com.google.code.gson:gson:2.10'
    provided 'com.formdev:flatlaf:3.5'
    provided 'org.kohsuke:github-api:1.322'
}


processResources {
    // Totally stolen from Fabric
    inputs.property "version", project.version

    filesMatching("*.plugin.json") {
        expand "version": project.version
    }
}

jar {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    manifest {
        attributes 'Main-Class': 'gg.paceman.tracker.launching.PaceManTrackerJarLaunch',
                'Multi-Release': true,
                'Implementation-Version': version
    }

    // Include dependencies marked as provided
    from configurations.provided.asFileTree.files.collect { zipTree(it) }
    // Main jar is not a plugin
    exclude "julti.plugin.json"
    exclude "jingle.plugin.json"
    exclude "gg/paceman/tracker/launching/PaceManTrackerJinglePluginInit.class"
    exclude "gg/paceman/tracker/launching/PaceManTrackerJultiPluginInit.class"
}

tasks.register('jultiPluginJar', Jar) {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    archiveBaseName = "${project.archives_base_name}-julti-plugin"
    from sourceSets.main.output
    exclude "jingle.plugin.json"
    exclude "gg/paceman/tracker/launching/PaceManTrackerJinglePluginInit.class"
}

tasks.register('jinglePluginJar', Jar) {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    archiveBaseName = "${project.archives_base_name}-jingle-plugin"
    from sourceSets.main.output
    exclude "julti.plugin.json"
    exclude "gg/paceman/tracker/launching/PaceManTrackerJultiPluginInit.class"
}

artifacts {
    archives jultiPluginJar
    archives jinglePluginJar
}